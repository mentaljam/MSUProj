if(NOT EXISTS "${CMAKE_CURRENT_LIST_DIR}/check_prerequisites.txt")
    message(FATAL_ERROR "The check_prerequisites.txt file does not exist")
endif()

message(STATUS "Getting prerequisites")
include(GetPrerequisites)

set(CMAKE_INSTALL_DO_STRIP @CMAKE_INSTALL_DO_STRIP@)
set(CMAKE_STRIP "@CMAKE_STRIP@")
set(GDAL_PREFIX "@GDAL_PREFIX@")
set(CMAKE_FIND_LIBRARY_PREFIXES "@CMAKE_FIND_LIBRARY_PREFIXES@")
set(CMAKE_FIND_LIBRARY_SUFFIXES "@CMAKE_FIND_LIBRARY_SUFFIXES@")

if("@RUNTIME_TYPE@" STREQUAL "GDAL")
    set(RUNTIME_REGEX ".*(gdal|geos).*")
    if(NOT @GDAL_PROJ4_STATIC@)
        set(PROJ4_NAMES proj proj4 proj-0)
        if(GDAL_PREFIX)
            find_library(PROJ4_LIBRARIES NAMES ${PROJ4_NAMES} PATHS "${GDAL_PREFIX}/bin" NO_DEFAULT_PATH)
        endif()
        if(NOT PROJ4_LIBRARIES)
            find_library(PROJ4_LIBRARIES NAMES ${PROJ4_NAMES} ${PROJ4_PATHS})
        endif()
        if(PROJ4_LIBRARIES)
            list(APPEND ALL_DEPENDENCIES ${PROJ4_LIBRARIES})
        endif()
    endif()
elseif("@RUNTIME_TYPE@" STREQUAL "GCC")
    set(RUNTIME_REGEX ".*(stdc|gcc|gomp).*")
elseif("@RUNTIME_TYPE@" STREQUAL "QT")
    set(RUNTIME_REGEX "(.*Qt|q|.*GLES).*")
endif()

file(STRINGS "${CMAKE_CURRENT_LIST_DIR}/check_prerequisites.txt" CHECK_PREREQUISITES)
foreach(TARGET ${CHECK_PREREQUISITES})
    get_prerequisites(${TARGET} DEPENDENCIES 1 1 "" "${GDAL_PREFIX};${GDAL_PREFIX}/bin;${GDAL_PREFIX}/lib")
    foreach(DEPENDENCY ${DEPENDENCIES})
        if("${DEPENDENCY}" MATCHES "${RUNTIME_REGEX}")
            gp_resolve_item(${TARGET} ${DEPENDENCY} "" "${GDAL_PREFIX};${GDAL_PREFIX}/bin;${GDAL_PREFIX}/lib" RESOLVED_ITEM)
            list(APPEND ALL_DEPENDENCIES ${RESOLVED_ITEM})
        endif()
    endforeach()
endforeach()

if(ALL_DEPENDENCIES)
    list(REMOVE_DUPLICATES ALL_DEPENDENCIES)
    foreach(DEPENDENCY ${ALL_DEPENDENCIES})
        get_filename_component(FILE_NAME ${DEPENDENCY} NAME)
        get_filename_component(RESOLVED_FILE ${DEPENDENCY} REALPATH)
        get_filename_component(EXT "${RESOLVED_FILE}" EXT)
        if(EXT STREQUAL ".dll")
            set(INSTALL_PATH "$ENV{DESTDIR}${CMAKE_INSTALL_PREFIX}/@INSTALL_PATH_BIN@")
        else()
            set(INSTALL_PATH "$ENV{DESTDIR}${CMAKE_INSTALL_PREFIX}/@INSTALL_PATH_LIB@")
        endif()
        file(INSTALL ${RESOLVED_FILE}
            DESTINATION ${INSTALL_PATH}
            RENAME ${FILE_NAME})
        if(EXISTS "${INSTALL_PATH}/${FILE_NAME}" AND CMAKE_INSTALL_DO_STRIP AND CMAKE_STRIP)
            execute_process(COMMAND ${CMAKE_STRIP} "${INSTALL_PATH}/${FILE_NAME}")
        endif()
    endforeach()
endif()
